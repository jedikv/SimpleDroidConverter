// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'dependencies.gradle'

buildscript {

    // Gradle will not find vars defined in an external file when referring to them
    // in the buildscript block, unless you link it from the buildscript block, too.
    apply from: 'dependencies.gradle'

    repositories {
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://clojars.org/repo/" }
        mavenCentral()
    }

    dependencies {
        // See dependencies.gradle for declaration of plugins.

        classpath gradlePlugins.android

        // Compile-time annotation processing.
        classpath gradlePlugins.apt
        //Dexcounter
        classpath gradlePlugins.keepSafeDexCount
        //Gradle plugin to discover dependency updates
        classpath gradlePlugins.gradleVersionsPlugin
        // Performance metrics library for Android development.
        classpath gradlePlugins.androidDevMetrics
        // Code coverage.
        classpath gradlePlugins.jacoco
    }
}

allprojects {
    //apply plugin: 'com.github.ben-manes.versions'
    repositories {
        jcenter()
        maven { url "https://clojars.org/repo/" }

    }

    // Workaround to prevent Gradle from stealing focus from other apps during tests run/etc.
    // https://gist.github.com/artem-zinnatullin/4c250e04636e25797165
    tasks.withType(JavaForkOptions) {
        jvmArgs '-Djava.awt.headless=true'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext.preDexLibs = !project.hasProperty('disablePreDex')
subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ('com.android.build.gradle.AppPlugin'.equals(plugin.class.name) || 'com.android.build.gradle.LibraryPlugin'.equals(plugin.class.name)) {
            // enable or disable pre-dexing
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}
